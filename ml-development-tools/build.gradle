// Copyright (c) 2022 MarkLogic Corporation

plugins {
    id "groovy"
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.0.0'
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm'  version '1.8.22'
}

dependencies {
	compileOnly gradleApi()
	implementation project(':marklogic-client-api')
	implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.8.22'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.15.2'
	implementation 'com.networknt:json-schema-validator:1.0.86'

	// Not yet migrating this project to JUnit 5. Will reconsider it once we have a reason to enhance
	// this project.
	testImplementation 'junit:junit:4.13.2'
	testImplementation 'xmlunit:xmlunit:1.6'
	testCompileOnly gradleTestKit()

	// Forcing usage of 3.4.0 instead of 3.2.0 to address vulnerability - https://security.snyk.io/vuln/SNYK-JAVA-COMSQUAREUPOKIO-5820002
	testImplementation 'com.squareup.okio:okio:3.4.0'
	testImplementation 'com.squareup.okhttp3:okhttp:4.11.0'

	// The jakarta.xml.bind implementation dependencies don't appear to come over from the implementation dependency
	// on the marklogic-client-api project, so they're declared here again.
	if (JavaVersion.current().isJava11Compatible()) {
		println "\nJava version is 11 or higher, so using jakarta.xml.bind 4.x; Java version: " + JavaVersion.current()
		testImplementation "jakarta.xml.bind:jakarta.xml.bind-api:4.0.0"
		testImplementation "org.glassfish.jaxb:jaxb-runtime:4.0.1"
		testImplementation "org.glassfish.jaxb:jaxb-core:4.0.1"
	} else {
		println "\nJava version is less than 11, so using jakarta.xml.bind 3.x; Java version: " + JavaVersion.current()
		testImplementation "jakarta.xml.bind:jakarta.xml.bind-api:3.0.1"
		testImplementation "org.glassfish.jaxb:jaxb-runtime:3.0.1"
		testImplementation "org.glassfish.jaxb:jaxb-core:3.0.1"
	}
}

// Added to avoid problem where processResources fails because - somehow - the plugin properties file is getting
// copied twice. This started occurring with the upgrade of Gradle from 6.x to 7.x.
tasks.processResources {
    duplicatesStrategy = "exclude"
}

task mlDevelopmentToolsJar(type: Jar, dependsOn: classes) {
    archivesBaseName = 'ml-development-tools'
}

pluginBundle {
    website = 'https://github.com/marklogic/java-client-api'
    vcsUrl = 'https://github.com/marklogic/java-client-api.git'
    tags = ['marklogic']
}

gradlePlugin {
    plugins {
        mlDevelopmentToolsPlugin {
            id = 'com.marklogic.ml-development-tools'
            implementationClass = 'com.marklogic.client.tools.gradle.ToolsPlugin'
            displayName = 'ml-development-tools MarkLogic Data Service Tools'
            description = 'ml-development-tools plugin for developing data services on MarkLogic'
        }
    }
}

publishing {
    publications {
        main(MavenPublication) {
            from components.java
        }
    }
	 repositories {
		maven {
			if(project.hasProperty("mavenUser")) {
				credentials {
					username mavenUser
					password mavenPassword
				}
			}
			url publishUrl
		}
	}
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}
compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

task generateTests(type: JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    main = 'com.marklogic.client.test.dbfunction.FntestgenKt'
    args = [ './src/test/', 'latest' ]
}

// Allows running "./gradlew test" without having to remember to generate the tests first.
test.dependsOn generateTests
