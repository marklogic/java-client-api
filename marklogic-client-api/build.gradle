// Copyright (c) 2022 MarkLogic Corporation

plugins {
    id 'maven-publish'
    id 'distribution'
}

group = 'com.marklogic'

description = "The official MarkLogic Java client API."

dependencies {
    if (JavaVersion.current().isJava9Compatible()) {
        println "\nJava version is 9 or higher, so using jakarta.xml.bind 4.0.0; Java version: " + JavaVersion.current()
        implementation "jakarta.xml.bind:jakarta.xml.bind-api:4.0.0"
        implementation "org.glassfish.jaxb:jaxb-runtime:4.0.1"
        implementation "org.glassfish.jaxb:jaxb-core:4.0.1"
    } else {
        println "\nJava version is less than 9, so using jakarta.xml.bind 3.0.1; Java version: " + JavaVersion.current()
        implementation "jakarta.xml.bind:jakarta.xml.bind-api:3.0.1"
        implementation "org.glassfish.jaxb:jaxb-runtime:3.0.1"
        implementation "org.glassfish.jaxb:jaxb-core:3.0.1"
    }

    implementation "com.squareup.okhttp3:okhttp:4.10.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.10.0"
    implementation "io.github.rburgst:okhttp-digest:2.7"
    implementation "com.sun.mail:javax.mail:1.6.2"
    implementation "javax.ws.rs:javax.ws.rs-api:2.1.1"
    implementation "org.slf4j:slf4j-api:1.7.36"
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.4'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.4'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.13.4'

    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.13.4'
    testImplementation "org.mockito:mockito-core:4.9.0"
    testImplementation "org.mockito:mockito-inline:4.9.0"

    // Cannot use >= 1.4 yet, requires Java 11
    testImplementation "ch.qos.logback:logback-classic:1.3.4"

    // Cannot use >= 2.6 yet, requires Java 11
    testImplementation "org.hsqldb:hsqldb:2.5.2"

    // Needed for tests that use SchemaFactory.newInstance
    // xerces:xercesImpl does not work in StructuredQueryBuilderTest
    testImplementation "org.opengis.cite.xerces:xercesImpl-xsd11:2.12-beta-r1667115"

    // These only seem needed for the cookbook examples, which would ideally be in a separate subproject so that
    // this project's Gradle file is simpler
    compileOnly "org.jdom:jdom2:2.0.6.1"
    compileOnly "dom4j:dom4j:1.6.1"
    compileOnly "com.google.code.gson:gson:2.10"
    compileOnly "net.sourceforge.htmlcleaner:htmlcleaner:2.26"
    compileOnly "com.opencsv:opencsv:4.6"
    compileOnly "org.geonames:geonames:1.0"
    compileOnly "org.springframework:spring-jdbc:5.3.24"
}

// Ensure that mlHost and mlPassword can override the defaults of localhost/admin if they've been modified
test {
    systemProperty "TEST_HOST", mlHost
    systemProperty "TEST_ADMIN_PASSWORD", mlPassword
    // Needed by the tests for the example programs
    systemProperty "EXAMPLE_HOST", mlHost
    systemProperty "EXAMPLE_ADMIN_PASSWORD", mlPassword
}

jar {
    exclude (
            'search.xsd', 'search-bindings.xjb', 'query-options-template.xml',
            'com/marklogic/client/example/**', 'data/**', 'Example.properties',
            'example/**', 'scripts/**', '*.txt', 'property.xsd',
            'restapi-bindings.xjb', 'security.xsd'
    )
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    exclude ('data/**', 'Example.properties', 'example/**', 'scripts/**',
             'property', '*.xsd', '*.xjb')
    from sourceSets.main.allSource
}

javadoc {
    maxMemory="6000m"
    options.overview = "src/main/javadoc/overview.html"
    options.windowTitle = "$rootProject.describedName $rootProject.version"
    options.docTitle = "$rootProject.describedName $rootProject.version"
    options.bottom = "Copyright &copy; 2022 MarkLogic Corporation"
    options.links = [ 'http://docs.oracle.com/javase/8/docs/api/' ]
    options.use = true
    exclude([
            '**/impl/**', '**/jaxb/**', '**/test/**'
    ])
// workaround for bug in options.docFilesSubDirs = true
    doLast{
        copy{
            from "${projectDir}/src/main/javadoc/doc-files"
            into "${buildDir}/docs/javadoc/doc-files"
        }
    }
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

Node pomCustomizations = new NodeBuilder(). project {
    name "$rootProject.describedName"
    packaging 'jar'
    textdescription "$project.description"
    url 'https://github.com/marklogic/java-client-api'

    scm {
        url 'git@github.com:marklogic/java-client-api.git'
        connection 'scm:git:git@github.com:marklogic/java-client-api.git'
        developerConnection 'scm:git:git@github.com:marklogic/java-client-api.git'
    }

    licenses {
        license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }

    developers {
        developer {
            name 'MarkLogic'
            email 'java-sig@marklogic.com'
            organization 'MarkLogic'
            organizationUrl 'https://www.marklogic.com'
        }
        developer {
            name 'MarkLogic Github Contributors'
            email 'general@developer.marklogic.com'
            organization 'Github Contributors'
            organizationUrl 'https://github.com/marklogic/java-client-api/graphs/contributors'
        }
    }
}

publishing {
    publications {
        mainJava(MavenPublication) {
            from components.java

            pom.withXml {
                asNode().append(pomCustomizations.packaging)
                asNode().append(pomCustomizations.name)
                asNode().appendNode("description", pomCustomizations.textdescription.text())
                asNode().append(pomCustomizations.url)
                asNode().append(pomCustomizations.licenses)
                asNode().append(pomCustomizations.developers)
                asNode().append(pomCustomizations.scm)
            }
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            if(project.hasProperty("mavenUser")) {
                credentials {
                    username mavenUser
                    password mavenPassword
                }
            }
            url publishUrl
        }
    }
}

distZip {
    archiveFileName = "$project.name-$rootProject.version-with-dependencies.zip"
    dependsOn javadoc
}

distributions {
    main {
        contents {
            from("..") {
                include("NOTICE.TXT")
            }
            from("src/main/resources") {
                include ("Readme.txt")
                include ("LICENSE.txt")
            }
            into ("example") {
                from ("src/main/resources") {
                    include ("data/**")
                    include ("scripts/**")
                    include ("Example.properties")
                }
                from ("src/main/resources/example") {
                    include ("README.txt")
                }
                from ("src/main/java/") {
                    include ("com/marklogic/client/example/cookbook/*.java")
                    include ("com/marklogic/client/example/handle/*.java")
                    include ("com/marklogic/client/example/extension/*.java")
                }
            }
            into ("doc/api") {
                from ("build/docs/javadoc")
            }
            into("lib") {
                from jar
                from(project.configurations.runtimeClasspath)
            }
        }
    }
}

task printClassPath() {
    doLast {
        println sourceSets.main.runtimeClasspath.asPath+':'+sourceSets.test.runtimeClasspath.asPath
    }
}

task generatePomForDependencyGraph(dependsOn: "generatePomFileForMainJavaPublication") {
    description = "Prepare for a release by making a copy of the generated pom file in the root directory so that it " +
            "can enable Github's Dependency Graph feature, which does not yet support Gradle"
    doLast {
        def preamble = '<?xml version="1.0" encoding="UTF-8"?>'
        def comment = "<!--\n" +
                "This file was generated via Gradle and is being used primarily for github's Dependency Graph feature.\n" +
                "It is not intended to be used to build this project.\n" +
                "-->"
        def fileText = file("build/publications/mainJava/pom-default.xml").getText()
        file("../pom.xml").setText(fileText.replace(preamble, preamble + comment))
    }
}

task testRows(type: Test) {
    description = "Run all 'rows' tests; i.e. those exercising Optic and Optic Update functionality"
    include "com/marklogic/client/test/rows/**"
}

task testXml(type: Test) {
    description = "Run a bunch of XML-related tests for smoke-testing on a particular JVM"
    include "com/marklogic/client/test/BufferableHandleTest.class"
    include "com/marklogic/client/test/EvalTest.class"
    include "com/marklogic/client/test/HandleAsTest.class"
    include "com/marklogic/client/test/JAXBHandleTest.class"
}