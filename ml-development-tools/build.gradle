// Copyright (c) 2022 MarkLogic Corporation

plugins {
    id "groovy"
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.0.0'
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm'  version '1.6.20'
}

dependencies {
    compileOnly gradleApi()
    implementation project(':marklogic-client-api')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.6.20'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.4'
    implementation 'com.networknt:json-schema-validator:1.0.73'

    testCompileOnly gradleTestKit()
    testImplementation 'com.squareup.okhttp3:okhttp:4.10.0'

    // The jakarta.xml.bind implementation dependencies don't appear to come over from the implementation dependency
    // on the marklogic-client-api project, so they're declared here again.
    if (JavaVersion.current().isJava9Compatible()) {
        testImplementation "jakarta.xml.bind:jakarta.xml.bind-api:4.0.0"
        testImplementation "org.glassfish.jaxb:jaxb-runtime:4.0.1"
        testImplementation "org.glassfish.jaxb:jaxb-core:4.0.1"
    } else {
        testImplementation "jakarta.xml.bind:jakarta.xml.bind-api:3.0.1"
        testImplementation "org.glassfish.jaxb:jaxb-runtime:3.0.1"
        testImplementation "org.glassfish.jaxb:jaxb-core:3.0.1"
    }
}

// Added to avoid problem where processResources fails because - somehow - the plugin properties file is getting
// copied twice. This started occurring with the upgrade of Gradle from 6.x to 7.x.
tasks.processResources {
    duplicatesStrategy = "exclude"
}

task mlDevelopmentToolsJar(type: Jar, dependsOn: classes) {
    archivesBaseName = 'ml-development-tools'
}

pluginBundle {
    website = 'https://github.com/marklogic/java-client-api'
    vcsUrl = 'https://github.com/marklogic/java-client-api.git'
    tags = ['marklogic']
}
gradlePlugin {
    plugins {
        mlDevelopmentToolsPlugin {
            id = 'com.marklogic.ml-development-tools'
            implementationClass = 'com.marklogic.client.tools.gradle.ToolsPlugin'
            displayName = 'ml-development-tools MarkLogic Data Service Tools'
            description = 'ml-development-tools plugin for developing data services on MarkLogic'
        }
    }
}

publishing {
    publications {
        main(MavenPublication) {
            from components.java
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}
compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

task setupTestServer(type: JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    main = 'com.marklogic.client.test.dbfunction.FntestconfKt'
    args = [ 'setup' ]
}
task generateTests(type: JavaExec) {
    dependsOn setupTestServer
    classpath = sourceSets.test.runtimeClasspath
    main = 'com.marklogic.client.test.dbfunction.FntestgenKt'
    args = [ './src/test/', 'latest' ]
}
task teardownTestServer(type: JavaExec) {
    dependsOn test
    classpath = sourceSets.test.runtimeClasspath
    main = 'com.marklogic.client.test.dbfunction.FntestconfKt'
    args = [ 'teardown' ]
}
