// Copyright Â© 2024 MarkLogic Corporation. All Rights Reserved.

test {
	useJUnitPlatform()
  testLogging{
    events 'started','passed', 'skipped'
  }
  /* For use in testing TestDatabaseClientKerberosFromFile */
  systemProperty "keytabFile", System.getProperty("keytabFile")
  systemProperty "principal", System.getProperty("principal")

	systemProperty "TEST_USE_REVERSE_PROXY_SERVER", testUseReverseProxyServer
}

/* The minimal number of tests that run in a sandbox environment */

task testSandbox(type:Test) {
  include 'com/marklogic/client/functionaltest/TestSandBox.class'
}

dependencies {
  implementation project (':marklogic-client-api')
  implementation 'org.skyscreamer:jsonassert:1.5.1'
  implementation 'org.slf4j:slf4j-api:1.7.36'
  implementation 'commons-io:commons-io:2.11.0'
  implementation 'com.squareup.okhttp3:okhttp:4.12.0'
  implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
  implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
  implementation "org.jdom:jdom2:2.0.6.1"
  implementation ("com.marklogic:ml-app-deployer:4.8.0") {
		exclude module: "marklogic-client-api"
	}

  testImplementation 'ch.qos.logback:logback-classic:1.3.14'
	testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
	testImplementation 'org.xmlunit:xmlunit-legacy:2.9.0'
	testImplementation 'org.apache.commons:commons-lang3:3.14.0'
	testImplementation 'org.apache.httpcomponents:httpclient:4.5.14'
}

task runFragileTests(type: Test) {
	useJUnitPlatform()
  description = "These are called 'fragile' because they'll pass when run by themselves, but when run as part of the " +
          "full suite, there seem to be one or more other fast functional tests that run before them and cause some of " +
          "their test methods to break. The Jenkinsfile thus calls these first before running the other functional " +
          "tests."
  include "com/marklogic/client/fastfunctest/TestQueryOptionBuilder.class"
  include "com/marklogic/client/fastfunctest/TestRawCombinedQuery.class"
  include "com/marklogic/client/fastfunctest/TestRawStructuredQuery.class"
}

task runFastFunctionalTests(type: Test) {
	useJUnitPlatform()
  description = "Run all fast functional tests that don't setup/teardown custom app servers / databases"
  include "com/marklogic/client/fastfunctest/**"
  // Exclude the "fragile" ones
  exclude "com/marklogic/client/fastfunctest/TestQueryOptionBuilder.class"
  exclude "com/marklogic/client/fastfunctest/TestRawCombinedQuery.class"
  exclude "com/marklogic/client/fastfunctest/TestRawStructuredQuery.class"
}

task runSlowFunctionalTests(type: Test) {
	useJUnitPlatform()
  description = "Run slow functional tests; i.e. those that setup/teardown custom app servers / databases"
  include "com/marklogic/client/datamovement/functionaltests/**"
  include "com/marklogic/client/functionaltest/**"
}

task runFunctionalTests {
  dependsOn(runFragileTests, runFastFunctionalTests, runSlowFunctionalTests)
}
runFastFunctionalTests.mustRunAfter runFragileTests
runSlowFunctionalTests.mustRunAfter runFastFunctionalTests
